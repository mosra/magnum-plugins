# Author: mosra <mosra@centrum.cz>
pkgname=magnum-plugins
pkgver=dev.gcc47
pkgrel=1
pkgdesc="Plugins for Magnum OpenGL graphics engine (built with GCC 4.7)"
arch=('i686' 'x86_64')
url="http://magnum.graphics"
license=('MIT')
depends=('magnum')
makedepends=('cmake' 'ninja' 'gcc47' 'qt4' 'devil' 'freetype2' 'harfbuzz' 'libjpeg' 'libpng' 'assimp')
options=(!strip)
provides=('magnum-plugins-git')

_rootdir=$startdir/../../

build() {
    # Disable flags unknown to GCC 4.7
    newcxxflags=$(echo $CXXFLAGS | sed s/-fstack-protector-strong.//g)
    export CXXFLAGS="$newcxxflags"

    if [ ! -d "$_rootdir/build-gcc47" ] ; then
        mkdir "$_rootdir/build-gcc47"
        cd "$_rootdir/build-gcc47"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-4.7 \
            -G Ninja
    fi

    cd "$_rootdir/build-gcc47"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -DWITH_ANYAUDIOIMPORTER=ON \
        -DWITH_ANYIMAGECONVERTER=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_ANYSCENEIMPORTER=ON \
        -DWITH_ASSIMPIMPORTER=ON \
        -DWITH_COLLADAIMPORTER=ON \
        -DWITH_DDSIMPORTER=ON \
        -DWITH_DEVILIMAGEIMPORTER=ON \
        -DWITH_DRFLACAUDIOIMPORTER=ON \
        -DWITH_DWAVAUDIOIMPORTER=ON \
        -DWITH_FREETYPEFONT=ON \
        -DWITH_HARFBUZZFONT=ON \
        -DWITH_JPEGIMPORTER=ON \
        -DWITH_MINIEXRIMAGECONVERTER=ON \
        -DWITH_OPENGEXIMPORTER=ON \
        -DWITH_PNGIMAGECONVERTER=ON \
        -DWITH_PNGIMPORTER=ON \
        -DWITH_STANFORDIMPORTER=ON \
        -DWITH_STBIMAGECONVERTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBTRUETYPEFONT=ON \
        -DWITH_STBVORBISAUDIOIMPORTER=ON
    ninja
}

check() {
    cd "$_rootdir/build-gcc47"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-gcc47"
    DESTDIR="$pkgdir/" ninja install
}
