# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum-plugins
pkgver=dev.wasm.webgl2
pkgrel=1
pkgdesc="Plugins for the Magnum C++11/C++14 graphics engine (Emscripten WebGL 2, wasm)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('emscripten-magnum=dev.wasm.webgl2')
makedepends=('cmake' 'emscripten' 'corrade' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-wasm-webgl2" ] ; then
        mkdir "$_rootdir/build-emscripten-wasm-webgl2"
        cd "$_rootdir/build-emscripten-wasm-webgl2"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten-wasm.cmake" \
            -DCMAKE_PREFIX_PATH=/usr/lib/emscripten/system \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-wasm-webgl2"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DWITH_DDSIMPORTER=ON \
        -DWITH_DEVILIMAGEIMPORTER=OFF \
        -DWITH_DRFLACAUDIOIMPORTER=ON \
        -DWITH_DRWAVAUDIOIMPORTER=ON \
        -DWITH_MINIEXRIMAGECONVERTER=ON \
        -DWITH_OPENGEXIMPORTER=ON \
        -DWITH_STANFORDIMPORTER=ON \
        -DWITH_STBIMAGECONVERTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBTRUETYPEFONT=ON \
        -DWITH_STBVORBISAUDIOIMPORTER=ON \
        -DWITH_TINYGLTFIMPORTER=ON \
        -DBUILD_TESTS=OFF
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-wasm-webgl2"

    # So tests are not even built (takes AGES) when the user doesn't want to
    cmake . -DBUILD_TESTS=ON
    ninja

    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten-wasm-webgl2"
    DESTDIR="$pkgdir/" ninja install
}
