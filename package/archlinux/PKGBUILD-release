# Author: mosra <mosra@centrum.cz>
pkgname=magnum-plugins
pkgver=dev.release
pkgrel=1
pkgdesc="Plugins for Magnum OpenGL graphics engine (debug+release libs)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('magnum')
makedepends=('cmake' 'ninja' 'qt4' 'freetype2' 'harfbuzz' 'libjpeg' 'libpng')
options=('!strip')
provides=('magnum-plugins-git')

build() {
    mkdir -p "$startdir/build"
    cd "$startdir/build"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_COLLADAIMPORTER=ON \
        -DWITH_FREETYPEFONT=ON \
        -DWITH_HARFBUZZFONT=ON \
        -DWITH_JPEGIMPORTER=ON \
        -DWITH_PNGIMPORTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBPNGIMAGECONVERTER=ON \
        -G Ninja
    ninja

    mkdir -p "$startdir/build-release"
    cd "$startdir/build-release"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON \
        -DBUILD_GL_TESTS=ON \
        -DWITH_ANYIMAGEIMPORTER=ON \
        -DWITH_COLLADAIMPORTER=ON \
        -DWITH_FREETYPEFONT=ON \
        -DWITH_HARFBUZZFONT=ON \
        -DWITH_JPEGIMPORTER=ON \
        -DWITH_PNGIMPORTER=ON \
        -DWITH_STBIMAGEIMPORTER=ON \
        -DWITH_STBPNGIMAGECONVERTER=ON \
        -G Ninja
    ninja
}

check() {
    cd "$startdir/build"
    ctest --output-on-failure

    cd "$startdir/build-release"
    ctest --output-on-failure
}

package() {
  cd "$startdir/build"
  DESTDIR="$pkgdir/" ninja install

  cd "$startdir/build-release"
  DESTDIR="$pkgdir/" ninja install/strip
}
